import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'scala'
    id 'application'
    id 'signing'
    id "com.lightbend.akka.grpc.gradle" version "2.1.4-6-42a72603"
    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()
}

group 'io.github.blyxa'
version '0.0.1-SNAPSHOT'

def scalaTarget = '2.13'
def scalaVersion = "2.13.8"
def akkaVersion="2.6.18"
def akkaHttp = "10.2.9"
def akkaGrpcRuntime = "2.1.4"

dependencies {
    implementation "org.scala-lang:scala-library:$scalaVersion"

    implementation 'ch.qos.logback:logback-classic:1.2.11'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.2'
    implementation "com.fasterxml.jackson.core:jackson-databind:2.13.2.2"
    implementation "com.fasterxml.jackson.module:jackson-module-scala_$scalaTarget:2.13.2"

    implementation 'com.konghq:unirest-java:3.13.7'
    implementation 'com.konghq:unirest-objectmapper-jackson:3.13.7'
    implementation 'org.jsoup:jsoup:1.14.3'
    implementation 'mysql:mysql-connector-java:8.0.28'
    implementation 'com.zaxxer:HikariCP:5.0.1'

    implementation "io.pebbletemplates:pebble:3.1.5"
    implementation "com.thesamet.scalapb:scalapb-runtime_$scalaTarget:0.10.9"

    implementation "com.lightbend.akka.grpc:akka-grpc-runtime_$scalaTarget:$akkaGrpcRuntime"
    implementation "com.typesafe.akka:akka-actor-typed_$scalaTarget:$akkaVersion"
    implementation "com.typesafe.akka:akka-discovery_$scalaTarget:$akkaVersion"
    implementation "com.typesafe.akka:akka-protobuf_$scalaTarget:$akkaVersion"
    implementation "com.typesafe.akka:akka-stream_$scalaTarget:$akkaVersion"
    implementation "com.typesafe.akka:akka-pki_$scalaTarget:$akkaVersion"
    implementation "com.typesafe.akka:akka-http-spray-json_$scalaTarget:$akkaHttp"
    implementation "com.typesafe.akka:akka-http_$scalaTarget:$akkaHttp"
    implementation "com.typesafe.akka:akka-http2-support_$scalaTarget:$akkaHttp"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
}

mainClassName = 'io.github.blyxa.rotala.ExampleMain'
java {
    withSourcesJar()
    withJavadocJar()
}

// Config akka grpc plugin to generate classes with access to metadata
// https://doc.akka.io/docs/akka-grpc/current/server/details.html#accessing-request-metadata
akkaGrpc {
    serverPowerApis = true
}
test {
    useJUnitPlatform()
}
processResources{
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
/**
 * Create a version file in resources
 */
task versionTxt()  {
    doLast {
        var d = new Date()
        new File("${buildDir}/resources/main/version-rotala.txt").text =
          "number=$version\n" +
          "build-time-epoch=${d.getTime()}\n"
          "build-time=${new SimpleDateFormat("yyyy-MM-dd HH:mm:ss Z").format(d)}\n"
    }
}
versionTxt.dependsOn classes
jar.dependsOn versionTxt

publishing {
    repositories {
        maven {
            name = "SonaTypeStaging"
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
        maven {
            name = "SonaTypeSnapshots"
            url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'Rotala'
                description = 'Web server boilerplate for Scala'
                url = 'https://blyxa.github.io/rotala'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'blyxa'
                        name = 'blyxa'
                        email = 'blyxaa@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:blyxa/rotala.git'
                    developerConnection = 'scm:git:git@github.com:blyxa/rotala.git'
                    url = 'https://github.com/blyxa/rotala'
                }
            }
        }
    }
}
signing {
    sign publishing.publications.maven
}
